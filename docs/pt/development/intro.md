# Ambientes de Desenvolvimento no NixOS

A reprodutibilidade do NixOS o torna ideal para a construÃ§Ã£o de ambientes de
desenvolvimento. No entanto, se vocÃª estÃ¡ acostumado com outras distribuiÃ§Ãµes (distros),
pode encontrar problemas porque o NixOS tem sua prÃ³pria lÃ³gica. Explicaremos brevemente
isso a seguir.

Nas seÃ§Ãµes seguintes, introduziremos como o ambiente de desenvolvimento funciona no NixOS.

## Criando um Ambiente de Shell Personalizado com `nix shell`

A maneira mais simples de criar um ambiente de desenvolvimento Ã© usar o `nix shell`. O
`nix shell` criarÃ¡ um ambiente de shell com o pacote Nix especificado instalado.

Aqui estÃ¡ um exemplo:

```shell
# hello nÃ£o estÃ¡ disponÃ­vel
â€º hello
hello: command not found

# Entrar em um ambiente com os pacotes 'hello' e `cowsay`
â€º nix shell nixpkgs#hello nixpkgs#cowsay

# hello agora estÃ¡ disponÃ­vel
â€º hello
Hello, world!

# cowsay tambÃ©m estÃ¡ disponÃ­vel
â€º cowsay "Hello, world!"
 _______
< hello >
 -------
        \   ^__^
         \  (oo)\_______
            (__)\       )\/\
                ||----w |
                ||     ||
```

O `nix shell` Ã© muito Ãºtil quando vocÃª apenas quer testar alguns pacotes ou criar
rapidamente um ambiente limpo.

## Criando um Ambiente de Desenvolvimento

O `nix shell` Ã© simples e fÃ¡cil de usar, mas nÃ£o Ã© muito flexÃ­vel. Para um ambiente de
desenvolvimento mais complexo, precisamos usar `pkgs.mkShell` e `nix develop`.

Podemos criar um ambiente de desenvolvimento usando `pkgs.mkShell { ... }` e abrir um
shell Bash interativo desse ambiente de desenvolvimento usando `nix develop`.

Para ver como o `pkgs.mkShell` funciona, vamos dar uma olhada
[em seu cÃ³digo-fonte](https://github.com/NixOS/nixpkgs/blob/nixos-23.05/pkgs/build-support/mkshell/default.nix).

```nix
{ lib, stdenv, buildEnv }:

# Uma espÃ©cie especial de derivaÃ§Ã£o que se destina apenas a ser consumida pelo
# nix-shell.
{ name ? "nix-shell"
, # uma lista de pacotes a serem adicionados ao ambiente do shell
  packages ? [ ]
, # propaga todas as entradas das derivaÃ§Ãµes fornecidas
  inputsFrom ? [ ]
, buildInputs ? [ ]
, nativeBuildInputs ? [ ]
, propagatedBuildInputs ? [ ]
, propagatedNativeBuildInputs ? [ ]
, ...
}@attrs:
let
  mergeInputs = name:
    (attrs.${name} or [ ]) ++
    (lib.subtractLists inputsFrom (lib.flatten (lib.catAttrs name inputsFrom)));

  rest = builtins.removeAttrs attrs [
    "name"
    "packages"
    "inputsFrom"
    "buildInputs"
    "nativeBuildInputs"
    "propagatedBuildInputs"
    "propagatedNativeBuildInputs"
    "shellHook"
  ];
in

stdenv.mkDerivation ({
  inherit name;

  buildInputs = mergeInputs "buildInputs";
  nativeBuildInputs = packages ++ (mergeInputs "nativeBuildInputs");
  propagatedBuildInputs = mergeInputs "propagatedBuildInputs";
  propagatedNativeBuildInputs = mergeInputs "propagatedNativeBuildInputs";

  shellHook = lib.concatStringsSep "\n" (lib.catAttrs "shellHook"
    (lib.reverseList inputsFrom ++ [ attrs ]));

  phases = [ "buildPhase" ];

  # ......

  # quando a construÃ§Ã£o distribuÃ­da estÃ¡ habilitada, prefira construir localmente
  preferLocalBuild = true;
} // rest)
```

`pkgs.mkShell { ... }` Ã© uma derivaÃ§Ã£o (derivation) especial (pacote Nix). Seu `name`,
`buildInputs` e outros parÃ¢metros sÃ£o personalizÃ¡veis, e `shellHook` Ã© um parÃ¢metro
especial que serÃ¡ executado quando `nix develop` entrar no ambiente.

Aqui estÃ¡ um `flake.nix` que define um ambiente de desenvolvimento com o Node.js 18
instalado:

```nix
{
  description = "A Nix-flake-based Node.js development environment";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-25.05";
  };

  outputs = { self , nixpkgs ,... }: let
    # o sistema deve corresponder ao sistema em que vocÃª estÃ¡ rodando
    # system = "x86_64-linux";
    system = "x86_64-darwin";
  in {
    devShells."${system}".default = let
      pkgs = import nixpkgs {
        inherit system;
      };
    in pkgs.mkShell {
      # criar um ambiente com nodejs_18, pnpm e yarn
      packages = with pkgs; [
        nodejs_18
        nodePackages.pnpm
        (yarn.override { nodejs = nodejs_18; })
      ];

      shellHook = ''
        echo "node `node --version`"
      '';
    };
  };
}
```

Crie uma pasta vazia, salve a configuraÃ§Ã£o acima como `flake.nix` e, em seguida, execute
`nix develop` (ou, mais precisamente, vocÃª pode usar `nix develop .#default`), A versÃ£o
atual do nodejs serÃ¡ exibida, e agora vocÃª pode usar `node`, `pnpm` e `yarn` sem
problemas.

## Usando zsh/fish/... em vez de bash

O `pkgs.mkShell` usa `bash` por padrÃ£o, mas vocÃª tambÃ©m pode usar `zsh` ou `fish`
adicionando `exec <your-shell>` no `shellHook`.

Aqui estÃ¡ um exemplo:

```nix
{
  description = "A Nix-flake-based Node.js development environment";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-25.05";
  };

  outputs = { self , nixpkgs ,... }: let
    # o sistema deve corresponder ao sistema em que vocÃª estÃ¡ rodando
    # system = "x86_64-linux";
    system = "x86_64-darwin";
  in {
    devShells."${system}".default = let
      pkgs = import nixpkgs {
        inherit system;
      };
    in pkgs.mkShell {
      # criar um ambiente com nodejs_18, pnpm e yarn
      packages = with pkgs; [
        nodejs_18
        nodePackages.pnpm
        (yarn.override { nodejs = nodejs_18; })
        nushell
      ];

      shellHook = ''
        echo "node `node --version`"
        exec nu
      '';
    };
  };
}
```

Com a configuraÃ§Ã£o acima, o `nix develop` entrarÃ¡ no ambiente REPL do nushell.

## Criando um Ambiente de Desenvolvimento com `pkgs.runCommand`

A derivaÃ§Ã£o criada pelo `pkgs.mkShell` nÃ£o pode ser usada diretamente, mas deve ser
acessada via `nix develop`.

Ã‰ realmente possÃ­vel criar um wrapper de shell contendo os pacotes necessÃ¡rios via
`pkgs.stdenv.mkDerivation`, que pode entÃ£o ser executado diretamente no ambiente.

Usar `mkDerivation` diretamente Ã© um pouco complicado, e o Nixpkgs fornece algumas funÃ§Ãµes
mais simples para nos ajudar a criar esses wrappers, como `pkgs.runCommand`.

Exemplo:

```nix
{
  description = "A Nix-flake-based Node.js development environment";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-25.05";
  };

  outputs = { self , nixpkgs ,... }: let
    # o sistema deve corresponder ao sistema em que vocÃª estÃ¡ rodando
    # system = "x86_64-linux";
    system = "x86_64-darwin";
  in {
    packages."${system}".dev = let
      pkgs = import nixpkgs {
        inherit system;
      };
      packages = with pkgs; [
          nodejs_20
          nodePackages.pnpm
          nushell
      ];
    in pkgs.runCommand "dev-shell" {
      # DependÃªncias que devem existir no ambiente de tempo de execuÃ§Ã£o
      buildInputs = packages;
      # DependÃªncias que sÃ³ devem existir no ambiente de construÃ§Ã£o
      nativeBuildInputs = [ pkgs.makeWrapper ];
    } ''
      mkdir -p $out/bin/
      ln -s ${pkgs.nushell}/bin/nu $out/bin/dev-shell
      wrapProgram $out/bin/dev-shell --prefix PATH : ${pkgs.lib.makeBinPath packages}
    '';
  };
}
```

Em seguida, execute `nix run .#dev` ou `nix shell .#dev --command 'dev-shell'`, VocÃª
entrarÃ¡ em uma sessÃ£o do nushell, onde poderÃ¡ usar os comandos `node` e `pnpm`
normalmente, e a versÃ£o do node serÃ¡ a 20.

O wrapper gerado desta forma Ã© um arquivo executÃ¡vel, que na verdade nÃ£o depende do
comando `nix run` ou `nix shell`.

Por exemplo, podemos instalar diretamente este wrapper atravÃ©s do
`environment.systemPackages` do NixOS e, em seguida, executÃ¡-lo diretamente:

```nix
{pkgs, lib, ...}:{

  environment.systemPackages = [
    # Instalar o wrapper no sistema
    (let
      packages = with pkgs; [
          nodejs_20
          nodePackages.pnpm
          nushell
      ];
    in pkgs.runCommand "dev-shell" {
      # DependÃªncias que devem existir no ambiente de tempo de execuÃ§Ã£o
      buildInputs = packages;
      # DependÃªncias que sÃ³ devem existir no ambiente de construÃ§Ã£o
      nativeBuildInputs = [ pkgs.makeWrapper ];
    } ''
      mkdir -p $out/bin/
      ln -s ${pkgs.nushell}/bin/nu $out/bin/dev-shell
      wrapProgram $out/bin/dev-shell --prefix PATH : ${pkgs.lib.makeBinPath packages}
    '')
  ];
}
```

Adicione a configuraÃ§Ã£o acima a qualquer MÃ³dulo NixOS, em seguida, implante-a com
`sudo nixos-rebuild switch`, e vocÃª poderÃ¡ entrar no ambiente de desenvolvimento
diretamente com o comando `dev-shell`, que Ã© a caracterÃ­stica especial de
`pkgs.runCommand` em comparaÃ§Ã£o com `pkgs.mkShell`.

CÃ³digo-fonte relacionado:

- [pkgs/build-support/trivial-builders/default.nix - runCommand](https://github.com/NixOS/nixpkgs/blob/nixos-25.05/pkgs/build-support/trivial-builders/default.nix#L21-L49)
- [pkgs/build-support/setup-hooks/make-wrapper.sh](https://github.com/NixOS/nixpkgs/blob/nixos-25.05/pkgs/build-support/setup-hooks/make-wrapper.sh)

## Entrar no Ambiente de ConstruÃ§Ã£o de Qualquer Pacote Nix

Agora vamos dar uma olhada em `nix develop`. Primeiro, leia a documentaÃ§Ã£o de ajuda
exibida por `nix develop --help`:

```
Name
    nix develop - run a bash shell that provides the build environment of a derivation

Synopsis
    nix develop [option...] installable
# ......
```

Ele nos diz que `nix develop` aceita um parÃ¢metro `installable`, o que significa que
podemos entrar no ambiente de desenvolvimento de qualquer pacote Nix instalÃ¡vel atravÃ©s
dele, nÃ£o apenas o ambiente criado por `pkgs.mkShell`.

Por padrÃ£o, `nix develop`tentarÃ¡ usar os seguintes atributos nas saÃ­das do flake:

- `devShells.<system>.default`
- `packages.<system>.default`

Se usarmos `nix develop /path/to/flake#<name>` para especificar o endereÃ§o do pacote flake
e o nome da saÃ­da do flake, entÃ£o `nix develop` tentarÃ¡ os seguintes atributos nas saÃ­das
do flake:

- `devShells.<system>.<name>`
- `packages.<system>.<name>`
- `legacyPackages.<system>.<name>`

Agora vamos testÃ¡-lo. Primeiro, teste para confirmar que nÃ£o temos `c++`, `g++` e outros
comandos relacionados Ã  compilaÃ§Ã£o no ambiente atual:

```shell
ryan in ğŸŒ aquamarine in ~
â€º c++
c++: command not found

ryan in ğŸŒ aquamarine in ~
â€º g++
g++: command not found
```

Em seguida, use `nix develop` para entrar no ambiente de construÃ§Ã£o do pacote `hello` no
`nixpkgs`:

```shell
# logar no ambiente de construÃ§Ã£o do pacote `hello`
ryan in ğŸŒ aquamarine in ~
â€º nix develop nixpkgs#hello

ryan in ğŸŒ aquamarine in ~ via â„ï¸  impure (hello-2.12.1-env)
â€º env | grep CXX
CXX=g++

ryan in ğŸŒ aquamarine in ~ via â„ï¸  impure (hello-2.12.1-env)
â€º c++ --version
g++ (GCC) 12.3.0
Copyright (C) 2022 Free Software Foundation, Inc.
This is free software; see the source for copying conditions.  There is NO
warranty; not even for MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.

ryan in ğŸŒ aquamarine in ~ via â„ï¸  impure (hello-2.12.1-env)
â€º g++ --version
g++ (GCC) 12.3.0
Copyright (C) 2022 Free Software Foundation, Inc.
This is free software; see the source for copying conditions.  There is NO
warranty; not even for MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
```

Podemos ver que a variÃ¡vel de ambiente `CXX` foi definida e que os comandos `c++`, `g++` e
outros agora podem ser usados normalmente.

AlÃ©m disso, tambÃ©m podemos chamar cada fase de construÃ§Ã£o do pacote `hello` normalmente:

> A ordem de execuÃ§Ã£o padrÃ£o de todas as fases de construÃ§Ã£o de um pacote Nix Ã©:
> `$prePhases unpackPhase patchPhase $preConfigurePhases configurePhase $preBuildPhases buildPhase checkPhase $preInstallPhases installPhase fixupPhase installCheckPhase $preDistPhases distPhase $postPhases`

```shell
# descompactar o cÃ³digo-fonte
ryan in ğŸŒ aquamarine in /tmp/xxx via â„ï¸  impure (hello-2.12.1-env)
â€º unpackPhase
unpacking source archive /nix/store/pa10z4ngm0g83kx9mssrqzz30s84vq7k-hello-2.12.1.tar.gz
source root is hello-2.12.1
setting SOURCE_DATE_EPOCH to timestamp 1653865426 of file hello-2.12.1/ChangeLog

ryan in ğŸŒ aquamarine in /tmp/xxx via â„ï¸  impure (hello-2.12.1-env)
â€º ls
hello-2.12.1

ryan in ğŸŒ aquamarine in /tmp/xxx via â„ï¸  impure (hello-2.12.1-env)
â€º cd hello-2.12.1/

# gerar Makefile
ryan in ğŸŒ aquamarine in /tmp/xxx/hello-2.12.1 via â„ï¸  impure (hello-2.12.1-env)
â€º configurePhase
configure flags: --prefix=/tmp/xxx/outputs/out --prefix=/tmp/xxx/outputs/out
checking for a BSD-compatible install... /nix/store/02dr9ymdqpkb75vf0v1z2l91z2q3izy9-coreutils-9.3/bin/install -c
checking whether build environment is sane... yes
checking for a thread-safe mkdir -p... /nix/store/02dr9ymdqpkb75vf0v1z2l91z2q3izy9-coreutils-9.3/bin/mkdir -p
checking for gawk... gawk
checking whether make sets $(MAKE)... yes
checking whether make supports nested variables... yes
checking for gcc... gcc
# ......
checking that generated files are newer than configure... done
configure: creating ./config.status
config.status: creating Makefile
config.status: creating po/Makefile.in
config.status: creating config.h
config.status: config.h is unchanged
config.status: executing depfiles commands
config.status: executing po-directories commands
config.status: creating po/POTFILES
config.status: creating po/Makefile

# construir o pacote
ryan in ğŸŒ aquamarine in /tmp/xxx/hello-2.12.1 via C v12.3.0-gcc via â„ï¸  impure (hello-2.12.1-env) took 2s
â€º buildPhase
build flags: SHELL=/run/current-system/sw/bin/bash
make  all-recursive
make[1]: Entering directory '/tmp/xxx/hello-2.12.1'
# ......
ranlib lib/libhello.a
gcc  -g -O2   -o hello src/hello.o  ./lib/libhello.a
make[2]: Leaving directory '/tmp/xxx/hello-2.12.1'
make[1]: Leaving directory '/tmp/xxx/hello-2.12.1'

# rodar o programa construÃ­do
ryan in ğŸŒ aquamarine in /tmp/xxx/hello-2.12.1 via C v12.3.0-gcc via â„ï¸  impure (hello-2.12.1-env)
â€º ./hello
Hello, world!
```

Esse uso Ã© principalmente para depurar o processo de construÃ§Ã£o de um pacote Nix, ou para
executar alguns comandos no ambiente de construÃ§Ã£o de um pacote Nix.

## `nix build`

O comando `nix build` Ã© usado para construir um pacote de software e cria um link
simbÃ³lico (symbolic link) chamado `result` no diretÃ³rio atual, que aponta para o resultado
da construÃ§Ã£o.

Aqui estÃ¡ um exemplo:

```bash
# Construir o pacote 'ponysay' do flake 'nixpkgs'
nix build "nixpkgs#ponysay"
# Usar o comando 'ponysay' construÃ­do
â€º ./result/bin/ponysay 'hey buddy!'
 ____________
< hey buddy! >
 ------------
     \
      \
       \
       â–„â–„  â–„â–„ â–„ â–„
    â–€â–„â–„â–„â–ˆâ–„â–„â–„â–„â–„â–ˆâ–„â–„â–„
   â–€â–„â–ˆâ–ˆâ–ˆâ–„â–„â–ˆâ–ˆâ–„â–ˆâ–ˆâ–„â–„â–ˆâ–ˆ
  â–„â–ˆâ–ˆâ–„â–ˆâ–ˆâ–ˆâ–„â–„â–ˆâ–ˆâ–„â–„â–„â–ˆâ–„â–ˆâ–ˆ
 â–ˆâ–„â–ˆâ–„â–ˆâ–ˆâ–„â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–„â–ˆâ–ˆ
  â–„â–„â–ˆâ–„â–ˆâ–„â–„â–„â–„â–„â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆ
 â–€â–€â–€â–„â–ˆâ–„â–ˆâ–„â–ˆâ–„â–„â–„â–„â–„â–ˆâ–ˆâ–ˆâ–ˆâ–ˆ         â–„   â–„
    â–€â–„â–ˆâ–ˆâ–ˆâ–ˆâ–„â–„â–„â–ˆâ–„â–ˆâ–„â–„â–ˆâ–ˆ       â–„â–„â–„â–„â–„â–ˆâ–„â–„â–„
    â–ˆâ–„â–ˆâ–ˆâ–„â–„â–„â–„â–ˆâ–ˆâ–ˆâ–„â–„â–„â–ˆâ–ˆ    â–„â–„â–„â–„â–„â–„â–„â–„â–„â–ˆâ–„â–„
    â–€â–„â–„â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–„â–„â–„â–ˆâ–ˆâ–ˆâ–ˆ    â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆ
       â–€â–€â–€â–€â–€â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–„â–„ â–„â–„â–„â–„â–„â–„â–„â–„â–„â–„â–ˆâ–ˆâ–„â–ˆâ–„â–„â–€
            â–ˆâ–ˆâ–„â–ˆâ–ˆâ–ˆâ–„â–„â–„â–„â–ˆâ–„â–„â–€  â–ˆâ–ˆâ–ˆâ–„â–ˆâ–„â–„â–„â–ˆâ–€
            â–ˆâ–„â–ˆâ–ˆâ–„â–„â–„â–„â–„â–ˆâ–ˆâ–ˆâ–ˆ   â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–„â–ˆâ–ˆ
            â–ˆâ–„â–ˆâ–ˆâ–ˆâ–„â–„â–ˆâ–ˆâ–ˆâ–ˆâ–ˆ    â–€â–ˆâ–ˆâ–ˆâ–„â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–„
            â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–€â–„â–„â–„â–ˆâ–„â–ˆ    â–ˆâ–„â–ˆâ–ˆâ–„â–„â–ˆâ–„â–ˆâ–„
           â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆ â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆ   â–€â–ˆâ–ˆâ–ˆâ–ˆâ–„â–ˆâ–ˆâ–ˆâ–ˆ
           â–€â–€â–ˆâ–„â–„â–„â–€ â–€â–€â–ˆâ–„â–„â–„â–€     â–€â–ˆâ–ˆâ–„â–„â–ˆâ–ˆâ–€â–ˆ
                                â–€  â–€â–€â–ˆ
```

## Usando `nix profile` para gerenciar Ambientes de Desenvolvimento e Ambientes de Entretenimento

`nix develop` Ã© uma ferramenta para criar e gerenciar mÃºltiplos ambientes de usuÃ¡rio e
alternar para diferentes ambientes quando necessÃ¡rio.

Ao contrÃ¡rio do `nix develop`, o `nix profile` gerencia o ambiente de sistema do usuÃ¡rio,
em vez de criar um ambiente de shell temporÃ¡rio. Portanto, Ã© mais compatÃ­vel com IDEs como
JetBrains IDE/VSCode e nÃ£o terÃ¡ o problema de nÃ£o conseguir usar o ambiente de
desenvolvimento configurado na IDE.

TODO: Isso estÃ¡ em andamento. Precisa ser revisado.

## Outros Comandos

Existem outros comandos como `nix flake init`, que vocÃª pode explorar em [New Nix
Commands][New Nix Commands]. Para informaÃ§Ãµes mais detalhadas, por favor, consulte a
documentaÃ§Ã£o.

## ReferÃªncias

- [pkgs.mkShell - nixpkgs manual](https://nixos.org/manual/nixpkgs/stable/#sec-pkgs-mkShell)
- [A minimal nix-shell](https://fzakaria.com/2021/08/02/a-minimal-nix-shell.html)
- [Wrapping packages - NixOS Cookbook](https://wiki.nixos.org/wiki/Nix_Cookbook#Wrapping_packages)
- [One too many shell, Clearing up with nix' shells nix shell and nix-shell - Yannik Sander](https://blog.ysndr.de/posts/guides/2021-12-01-nix-shells/)
- [Shell Scripts - NixOS Wiki](https://wiki.nixos.org/wiki/Shell_Scripts)

[New Nix Commands]: https://nixos.org/manual/nix/stable/command-ref/new-cli/nix.html
